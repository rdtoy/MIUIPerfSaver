plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

project.ext {
    applicationId = "com.rdstory.miuiperfsaver"
    configProviderAuthority = "com.rdstory.miuiperfsaver.config_provider"
}

android {
    compileSdk 31

    defaultConfig {
        applicationId project.ext.applicationId
        minSdk 29
        targetSdk 31
        versionCode 1030
        versionName "1.0.3"
        resConfigs "zh-rCN"
        manifestPlaceholders = [
                configProviderAuthority: project.ext.configProviderAuthority
        ]
        buildConfigField "String", "CONFIG_PROVIDER_AUTHORITY", "\"${project.ext.configProviderAuthority}\""
        resValue "string", "app_name", "@string/app_name_release"
    }

    signingConfigs {
        release {
            if (System.getenv("KEYSTORE_PATH")) {
                storeFile file(System.getenv("KEYSTORE_PATH"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEYSTORE_KEY_ALIAS")
                keyPassword System.getenv("KEYSTORE_KEY_PASSWORD")
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            resValue "string", "app_name", "@string/app_name_debug"
            manifestPlaceholders = [
                    configProviderAuthority: project.ext.configProviderAuthority + ".debug"
            ]
            buildConfigField "String", "CONFIG_PROVIDER_AUTHORITY", "\"${project.ext.configProviderAuthority + ".debug"}\""

            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (System.getenv("KEYSTORE_PATH")) {
                signingConfig signingConfigs.release
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation "com.google.android.material:material:1.6.0-beta01"
    implementation 'androidx.navigation:navigation-runtime-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'
}
